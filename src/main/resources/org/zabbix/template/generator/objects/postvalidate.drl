package org.zabbix.template.generator.objects
global org.slf4j.Logger logger;


rule "Validation rule 1: you should have both discoveryRule and alarmObject defined"
	agenda-group "postvalidate"
when
	$m : Metric (discoveryRule != null, alarmObject == null )
then
    logger.error("Please define 'alarmObject' for "+$m);
end


rule "Validation rule 2: check key"
	agenda-group "postvalidate"
when
	$m : Metric (key == null)
then
    logger.error("There is no 'key' for "+$m.getName());
end




rule "Validation rule 3: valueMap mentioned in metric should be defined"
	agenda-group "postvalidate"
when
	$m: Metric($vm_name : valueMap != null)
	$t: InputJSON( )
	not ValueMap(name == $vm_name) from $t.valueMaps
then
    logger.warn("No value map found for " + $vm_name + ". It used in " +$m.getId());
end


rule "Validation rule 3: check Type"
	agenda-group "postvalidate"
when
	$m: Metric(type == null)
then
    logger.error("No 'type' defined for" + $m.getId());
end
