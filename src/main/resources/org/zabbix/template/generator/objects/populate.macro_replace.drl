package org.zabbix.template.generator.objects
global org.slf4j.Logger logger;
/*
METRICS
*/
//
rule "Rule: replaces ALARM_OBJECT macro in metrics"
	agenda-group "populate"
	no-loop
when    
    $m : Metric ($key: key != null,  $alarmObject: alarmObject != null) 
then
    if ($m.getDescription() !=null) { 
    	$m.setDescription( $m.getDescription().replaceAll("ALARM_OBJECT",$alarmObject));
    }
    $m.setName( $m.getName().replaceAll("ALARM_OBJECT",$alarmObject));
end

rule "Rule: replaces ALARM_OBJECT_TYPE macro in metrics"
	agenda-group "populate"
	no-loop
when    
    $m : Metric ($key: key != null, $alarmObjectType: alarmObjectType != null) 
then
    if ($m.getDescription() !=null) { 
    	$m.setDescription( $m.getDescription().replaceAll("ALARM_OBJECT_TYPE",$alarmObjectType));
    }
    $m.setName( $m.getName().replaceAll("ALARM_OBJECT_TYPE",$alarmObjectType));
end



/*
TRIGGERS
*/
//replaces TEMPLATE_NAME:METRIC name
rule "Rule: generate each trigger expression"
	agenda-group "populate"
	no-loop
when
    
    $m : Metric ($key: key != null, $triggers: triggers)
    $t : Template(metricsRegistry contains $m)
    $tr: Trigger ($e: expression!=null) from $triggers 
then
    logger.debug("Generating trigger expression (changing TEMPLATE_NAME:METRIC) for "+ $tr.getName());
    $tr.setExpression( $e.replaceAll("TEMPLATE_NAME:METRIC",$t.getName()+":"+$key));
    if ($tr.getRecoveryExpression() != null) {
    	$tr.setRecoveryExpression( $tr.getRecoveryExpression().replaceAll("TEMPLATE_NAME:METRIC",$t.getName()+":"+$key));
    }
end


/*
TRIGGERS REPLACE METRICS in expressions
*/
//replaces TEMPLATE_NAME:METRIC name
rule "Rule: replace metrics in expression"
	agenda-group "populate"
	no-loop
when
    
    $m : Metric ($key: key != null, $triggers: triggers)
    $t : Template(metricsRegistry contains $m)
    $tr: Trigger ($e: expression!=null, $metricsUsed: metricsUsed) from $triggers
    
    $m2: Metric (key != null, discoveryRule == $m.discoveryRule, $m2.prototype memberOf $metricsUsed ) 
then
    logger.warn("REPLACING metric "+$m2.getPrototype()+" in triggers: "+ $tr.getName());
    
    $tr.setExpression( $e.replaceAll("TEMPLATE_NAME:__"+$m2.getPrototype()+"__",$t.getName()+":"+$m2.getKey()));
    if ($tr.getRecoveryExpression() != null) {
    	$tr.setRecoveryExpression( $tr.getRecoveryExpression().replaceAll("TEMPLATE_NAME:__"+$m2.getPrototype()+"__",$t.getName()+":"+$m2.getKey()));
    }    
end


//
rule "Rule: replaces ALARM_OBJECT macro in expressions"
	agenda-group "populate"
	no-loop
when    
    $m : Metric ($key: key != null, $triggers: triggers, $alarmObject: alarmObject != null)
    $tr: Trigger () from $triggers 
then
    logger.debug("Generating trigger expression (changing ALARM_OBJECT) for "+ $tr.getName());
    if ($tr.getRecoveryExpression() !=null) { 
    	$tr.setRecoveryExpression( $tr.getRecoveryExpression().replaceAll("ALARM_OBJECT",$alarmObject));
    }
    if ($tr.getDescription() !=null) { 
    	$tr.setDescription( $tr.getDescription().replaceAll("ALARM_OBJECT",$alarmObject));
    }
    $tr.setExpression( $tr.getExpression().replaceAll("ALARM_OBJECT",$alarmObject));
    $tr.setName( $tr.getName().replaceAll("ALARM_OBJECT",$alarmObject));
end

rule "Rule: replaces ALARM_OBJECT_TYPE macro in expressions"
	agenda-group "populate"
	no-loop
when    
    $m : Metric ($key: key != null, $triggers: triggers, $alarmObjectType: alarmObjectType != null)
    $tr: Trigger () from $triggers 
then
    logger.debug("Generating trigger expression (changing ALARM_OBJECT_TYPE) for "+ $tr.getName());
    if ($tr.getRecoveryExpression() !=null) { 
    	$tr.setRecoveryExpression( $tr.getRecoveryExpression().replaceAll("ALARM_OBJECT_TYPE",$alarmObjectType));
    }
    if ($tr.getDescription() !=null) { 
    	$tr.setDescription( $tr.getDescription().replaceAll("ALARM_OBJECT_TYPE",$alarmObjectType));
    }
    $tr.setExpression( $tr.getExpression().replaceAll("ALARM_OBJECT_TYPE",$alarmObjectType));
    $tr.setName( $tr.getName().replaceAll("ALARM_OBJECT_TYPE",$alarmObjectType));
end