package org.zabbix.template.generator.objects
;
global String lang;

/*
This will found trigger pairs (inside single metric ONLY(fix?)). If there is a trigger mentioned in dependsOn - add it to dependsOn
For example. This doesn't catch ICMP Ping dependencies inside ICMP Ping template
*/

rule "Generate each trigger dependency"
	agenda-group "populate.trigger.dependencies"
    dialect "mvel"
	no-loop
when
	$m : Metric ($triggers: triggers)
	$tr: Trigger ($dependsOn: dependsOn) from $triggers
	
	$m2 : Metric ($triggers2: triggers, $m.discoveryRule == discoveryRule, $m.alarmObject == alarmObject)
    $tr2: Trigger (
        $dependsOn contains id
        ,id != "noping"
     ) from $triggers2
then
    logger.info(marker,"Found dependency "+$tr2.id+"("+$tr2.name+") for "+$tr.id+"("+$tr.name+")");
    modify($tr){
        getDependencies().add(new TriggerDependency($tr2.name, $tr2.expression, $tr2.recoveryExpression));
    }

end

/*
TODO
This is static that should be removed in the future
*/
rule "Static rule to add ping dependency into some templates"
	agenda-group "populate.trigger.dependencies"
	no-loop
when
	$m : Metric ($triggers: triggers)
    $tr2: Trigger (dependsOn contains "noping") from $triggers
then
    logger.info(marker,"Found static dependency "+$tr2.getId()+" for noping");

        if(lang.equals("EN")){

                $tr2.getDependencies().add(new TriggerDependency("Unavailable by ICMP ping", "{TEMPLATE_NAME:icmpping.max(#3)}=0", null));

        } else if(lang.equals("RU")){

                $tr2.getDependencies().add(new TriggerDependency("Нет ответа на ICMP ping", "{TEMPLATE_NAME:icmpping.max(#3)}=0", null));

        }

end